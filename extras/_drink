#compdef drink drnk

_drink_kinds () {
    local objects
    _message "select kind"
    objects=(${(s: :)SUPPORTED_KINDS})
    compadd $objects
}

_drink_targets () {
    local targets
        typeset -aU all_targets
    _message "select target"
    targets=(global "$HOME/$DRINKDIR"/*/by-target/*(/:t))
    compadd $targets
}

_drink_vars () {
    local vars
    _message "select variable"
    vars=(${${(f)${"$(drink -u)"}}//=*/})
    compadd $vars
}

_drink_importable () {
    _message "files to import"
    case "$opt_args[-k]"
    in
        conf)
            _files -W ~/$CONFDIR -g ".*(.) .**/*(.)"
            ;;
        bin)
            _files -W ~/$BINDIR -g "*(.)"
            ;;
        zfunc)
            _files -W ~/$ZFUNCDIR -g "*(.)"
            ;;
        py)
            _files -W ~/$PYDIR -g "*(.)"
            ;;
        as)
            _files -W ~/$APPSUPPDIR -g "**/*(.)"
            ;;
        *)
            _files
            ;;
    esac
}

_drinkargs () {
    _arguments \
        {-v,--verbose}'[be more verbose sometimes]' \
        {-q,--quiet}'[be more quiet sometimes]' \
        {-d,--debug}'[turn on debugging output]' \
        {-h,--help}'[show help]' \
        {-k,--kind}'[select kind]:kind:_drink_kinds' \
        {-t,--target}'[select target]:target:_drink_targets' \
        - readme \
        {-r,--readme}'[show readme]' \
        - begin \
        {-b,--begin}'[initialize drink]' \
        - import \
        {-i,--import}'[import files into drink]:file(s) to import:_drink_importable' \
        - link \
        {-l,--link}'[generate symlinks for tracked files]' \
        - show_untracked \
        {-s,--show}'[show untracked files]' \
        - show_changed \
        {-c,--changed}'[show changed drink objects]' \
        - git_menu \
        {-g,--git}'[git menu]' \
        - version \
        {-V,--version}'[show version]' \
        - dump \
        {-u,--dump}'[dump config]:variable:_drink_vars'
}

# Use the command that was used to trigger the completion.
# Probably either "drnk" or "drink".
source =($words[1] -u)
_drinkargs "$@"
